&НаКлиенте
Процедура ВебХукИзменить(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Сообщить("Перед установкой или отключением вебхука необходимо записать кабинет");
		Возврат;
	КонецЕсли;

	ИзменитьВебХук(Объект.Ссылка, Объект.ВебХукСсылка, Объект.ВебХукВключен);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьБаланс(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Сообщить("Перед получением баланса необходимо записать кабинет");
		Возврат;
	КонецЕсли;
	
	ПолучитьБалансНаСервере(Объект.Ссылка);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьБалансНаСервере(Кабинет)
	
	Баланс = АвитоСервер.ПолучитьБалансКошелька(Кабинет);
	Если Баланс <> Неопределено Тогда
		Сообщить("Деньги: " + Баланс.real + ". Бонусы: " + Баланс.bonus);
	Иначе
		Сообщить("Что-то пошло не так! Обратитесь в техническую поддержку");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИсторию(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Сообщить("Перед получением статистики необходимо записать кабинет");
		Возврат;
	КонецЕсли;
	ТД = ПолучитьСтатистикуНаСервере(Объект.Ссылка, Новый Структура("ДатаНачала, ДатаОкончания", ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания));
	ТД.Показать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтатистикуНаСервере(Кабинет, ПериодОтчета)
	
	ТаблицаОпераций = АвитоСервер.ПолучитьИсториюОперацийЗаПериод(Кабинет, ПериодОтчета);
	Возврат СформироватьТабДокИстории(ТаблицаОпераций);
	
КонецФункции

&НаСервереБезКонтекста
Функция СформироватьТабДокИстории(ТаблицаОпераций) Экспорт
	
	Макет = Справочники.Кабинеты.ПолучитьМакет("ИсторияОпераций");
	ТД = Новый ТабличныйДокумент();
	Область = Макет.ПолучитьОбласть("ОбластьШапка");
	ТД.Вывести(Область);	
	Для каждого Стр из ТаблицаОпераций Цикл
		Область = Макет.ПолучитьОбласть("ОбластьОперации");
		ЗаполнитьЗначенияСвойств(Область.Параметры, Стр);
		ТД.Вывести(Область);
	КонецЦикла;
	Возврат ТД;
КонецФункции


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовокВебХук();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокВебХук()
	
	Элементы.ФормаВебХукИзменить.Заголовок = ?(Объект.ВебХукВключен, "Отключить вебхук", "Включить вебхук");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьВебХук(Кабинет, ВебхукСсылка, Отключить)
	
	Если АвитоСервер.ИзменитьВебХук(Кабинет, ВебхукСсылка, Отключить) Тогда
		Отключить = НЕ Отключить;
	КонецЕсли;
			
КонецПроцедуры